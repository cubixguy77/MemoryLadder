apply plugin: 'com.android.application'
android {

    namespace 'com.mastersofmemory.memoryladder'

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId "com.mastersofmemory.memoryladder"
        minSdkVersion 19
        compileSdk 34
        targetSdkVersion 34
        multiDexEnabled true
        versionCode 33
        versionName "2.3.0"
        vectorDrawables.useSupportLibrary true
    }

    flavorDimensions = ["env", "store"]
    productFlavors {
        dev {
            dimension "env"
            versionNameSuffix "-dev"
        }

        prod {
            dimension "env"
        }

        google {
            dimension "store"
            buildConfigField "boolean", "lockable", "true"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    buildFeatures {
        dataBinding true
        viewBinding true
        buildConfig = true
    }
    lint {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation 'com.android.billingclient:billing:6.1.0'
    implementation 'com.jjoe64:graphview:4.2.1'
    implementation 'com.adityak:browsemyfiles:1.7'
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
}

apply plugin: 'com.google.gms.google-services'

def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword
